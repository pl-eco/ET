// Assumes polyglot.jar is in classpath
include "polyglot/ext/jl5/parse/jl5.ppg"

package et.parse;

import polyglot.util.*;
import polyglot.parse.VarDeclarator;
import polyglot.ast.*;
import polyglot.types.*;
import polyglot.lex.*;

import polyglot.ext.jl5.parse.FlagAnnotations;
import polyglot.ext.jl5.parse.JL5Name;

import et.types.etTypeSystem;
import et.ast.etNodeFactory;
import polyglot.parse.Name;

import et.parse.ETName;
import et.ast.*;
import et.types.*;
import et.util.Names;
import et.linersolve.ETValueHolder;

import java.util.*;

parser Grm extends polyglot.ext.jl5.parse.Grm {:
    public final etTypeSystem ts;
    public final etNodeFactory nf;

    public Grm(Lexer l, TypeSystem t, NodeFactory n, ErrorQueue q) {
        super(l, t, n, q);
        ts = (etTypeSystem) t;
        nf = (etNodeFactory) n;
    }     
:};

terminal Token MODES; //MODE refers to mode
terminal Token PHASES;
terminal Token PARORD;
terminal Token LTP;
terminal Token ATMODE;
terminal Token ATPHASE;
terminal Token ADAPT;
terminal et.parse.ModeLiteral MODE_LITERAL;
terminal Token MODEVT;
//terminal Token MSWITCH;
terminal Token NOSCALE;
terminal Token PHASESUPER;

/* dynamic related syntax */
terminal Token ATTRIBUTE;
terminal Token ATTRIBUTER;
terminal Token RECONSTRUCT;
terminal Token RECONSTRUCTOR;
terminal Token AS;
terminal Token MPATTERN;
terminal Token MPAT_APP;

//class level decls
non terminal EnergyFlags at_opt, energies, energy;
non terminal Adapt_c adapt_expression;
non terminal CanonicalTypeNode mode_type;
non terminal List modes_declarations_opt;
non terminal List mode_body_declarations;
non terminal List mode_body_declaration;
non terminal List phases_declarations_opt;
non terminal List phase_body_declarations;
non terminal List phase_body_declaration;

/* epolicy decl */
/*
terminal Token EPOLICY;
terminal Token ESHIFT;
non terminal List epolicy_declarations_opt;
non terminal List epolicy_body_declarations;
non terminal List epolicy_body_declaration;
*/

//  The below is for attribute structure
non terminal List mswitch_block_statement_groups;
non terminal List mswitch_block_statement_group;
non terminal List mswitch_block;
non terminal ModeLit_c mswitch_label;
terminal Token DETERMINE;

/* DYNAMIC RELATED SYNTAX */
non terminal String static_name;
non terminal String dynamic_name;
non terminal MPatternElement mpattern_element;
non terminal List mpattern_elements;
non terminal MPattern_Init_c mpattern_init;
non terminal MPatternApp mpattern_application;
non terminal Reconstruct_c reconstruct_expression;
non terminal Attribute_c attribute_expression;

/* where syntax in method_header */
terminal Token WHERE;
non terminal List where_opt;
non terminal List mode_relation_list;
non terminal List mode_relation;
non terminal String energy_name;


terminal Token USING;
non terminal Stmt using_statement;

start with goal;

/* using syntax */

using_statement::=
		USING:a LPAREN name:b RPAREN block:c
		{: RESULT = parser.nf.Using(parser.pos(a, c), b.toExpr(),
				new UsingBlock_c(c, UsingBlock_c.Type.THEN), null); :}
	|
		USING:a LPAREN name:b RPAREN block:c ELSE block:d
		{: RESULT = parser.nf.Using(parser.pos(a, d), b.toExpr(),
				new UsingBlock_c(c, UsingBlock_c.Type.THEN),
				new UsingBlock_c(d, UsingBlock_c.Type.ELSE)); :}
;


extend statement::=
		using_statement:a
		{: RESULT = a; :}
	;

// Energy type syntax
at_opt::=
       {: RESULT = EnergyFlags.createDefault(); :}
    | 
		energies:a
            {: RESULT = a; :}
    ;
			
energies ::=
        energy:a 
            {: RESULT = a; :}
    |   energies:a energy:b
			{: if (a.intersects(b)) parser.die(parser.position());
               RESULT = a.set(b); :}
    ;
			   
energy ::=
        // @mode(?) or @mode(?m)	
  		ATMODE LPAREN energy_name:a RPAREN
		{:	RESULT = EnergyFlags.createEType(ETTYPE.MODE, a); :}
	|   
	    // @mode<CONSTANT>
	    ATMODE LT IDENTIFIER:a GT
		{:  RESULT = EnergyFlags.createEType(ETTYPE.MODE, a.getIdentifier()); :}
	|	ATPHASE LPAREN name:a RPAREN
            {: 
            RESULT = EnergyFlags.createEType(ETTYPE.PHASE, a.toString()); :} 
    |	ATPHASE LPAREN PHASESUPER:a RPAREN
            {: 
            RESULT = EnergyFlags.createEType(ETTYPE.PHASE, "_super"); :}
	|	ATPHASE LPAREN QUESTION RPAREN
		{:
		RESULT = EnergyFlags.createEType(ETTYPE.PHASE, EnergyFlags.ANONYMOUS); :}
	;
	
dynamic_name ::=
       //question mark
       	QUESTION
       		{:  RESULT = EnergyFlags.ANONYMOUS; :}
      | QUESTION IDENTIFIER:a
      		{:  RESULT = "?" + a.getIdentifier(); :}
      		;
      		
static_name ::=
	NOT IDENTIFIER:a
		{:  RESULT = "!" + a.getIdentifier(); :}
	;

//?, ?m, !m, m
energy_name ::=
       dynamic_name:a
            {: RESULT = a; :}
	| static_name:a
		{: RESULT = a; :}
      | IDENTIFIER:a
            {: RESULT = a.getIdentifier(); :}
      ;
      

override simple_name ::=
                    // Name
        IDENTIFIER:a at_opt:b
            {: 
            	RESULT = new ETName(parser.nf, parser.ts, parser.pos(a), parser.nf.Id(parser.pos(a), a.getIdentifier()), b.getFlags()); 
            :}
    ;
    
override qualified_name ::=
                    // Name
        name:a DOT IDENTIFIER:b at_opt:c
            {: 
            	RESULT = new ETName(parser.nf, parser.ts, parser.pos(a, b), a, parser.nf.Id(parser.pos(b), b.getIdentifier()), c.getFlags()); 
            :}
    ;

/*
 * The reason to override this syntax is to record position info for value mappint
 */ 
override class_instance_creation_expression ::=
		NEW:a class_or_interface_type:b LPAREN:c argument_list_opt:d RPAREN:e class_body_opt:f
        {: RESULT = parser.nf.New(parser.pos(a), b, d, f); :}
	|	NEW:a type_arguments:b class_or_interface_type:c LPAREN:d argument_list_opt:e RPAREN:f class_body_opt:g
	    // Instantiating type arguments    
        {: RESULT = parser.nf.New(parser.pos(a), b, c, e, g); :}
	|	primary:a DOT:b NEW:c type_arguments_opt:d IDENTIFIER:e at_opt:k type_arguments_opt:f LPAREN:g argument_list_opt:h RPAREN:i class_body_opt:j
        // Instantiating type arguments    
        {: 
           TypeNode aa = (new ETName(parser.nf, parser.ts, parser.pos(e), parser.nf.Id(parser.pos(e), e.getIdentifier()), k.getFlags())).toType();
           TypeNode tn = parser.nf.AmbTypeInstantiation(parser.pos(e, f), aa, f); 
           RESULT = parser.nf.New(parser.pos(a), a, d, tn, h, j); :}
	|	name:a DOT:b NEW:c type_arguments_opt:d IDENTIFIER:e at_opt:k type_arguments_opt:f LPAREN:g argument_list_opt:h RPAREN:i class_body_opt:j
	    // Instantiating type arguments    
        {: 
           TypeNode aa = (new ETName(parser.nf, parser.ts, parser.pos(e), parser.nf.Id(parser.pos(e), e.getIdentifier()), k.getFlags())).toType();
           TypeNode tn = parser.nf.AmbTypeInstantiation(parser.pos(e, f), aa, f);
           RESULT = parser.nf.New(parser.pos(a), a.toExpr(), d, tn, h, j); :}
;
 
extend modifier ::=
        NOSCALE:a
            {: RESULT = ETFlags.NOSCALE; :}
    ;
    
extend class_or_interface_type ::=
                    // mpattern<type>
        MPATTERN LT type:a  GT
            {: RESULT = parser.nf.MPattern(parser.pos(a), a); :}
    ;

// mode VT
extend type ::=
                   // TypeNode 	
	MODEVT:a
            {: RESULT = parser.nf.CanonicalTypeNode(parser.pos(a), parser.ts.modeVT()); :}
    ;  	
    
extend literal ::=
      MODE_LITERAL:a
            {: RESULT = parser.nf.ModeLit(parser.pos(a), a.getValue()); :}
    ;
 
override method_header ::=
        modifiers_or_annotations_opt:a type:b IDENTIFIER:d LPAREN
                formal_parameter_list_opt:e RPAREN:f dims_opt:g where_opt:i throws_opt:h
        {: RESULT = parser.nf.MethodDecl(parser.pos(b, f, d), a.flags(), a.annotations(), parser.array(b, g.intValue()), parser.nf.Id(parser.pos(d), d.getIdentifier()), e, h, null, new LinkedList<ParamTypeNode>(), i); :}
        |       
    modifiers_or_annotations_opt:a LT:b type_parameter_list_1:c type:d IDENTIFIER:i LPAREN
                formal_parameter_list_opt:e RPAREN:f dims_opt:g where_opt:j throws_opt:h
        {: RESULT = parser.nf.MethodDecl(parser.pos(d, f, i), a.flags(), a.annotations(), parser.array(d, g.intValue()), parser.nf.Id(parser.pos(i), i.getIdentifier()), e, h, null, c, j); :}
        |       
    modifiers_or_annotations_opt:a VOID:b IDENTIFIER:c LPAREN
                formal_parameter_list_opt:e RPAREN:f dims_opt:g where_opt:i throws_opt:h
        {: RESULT = parser.nf.MethodDecl(parser.pos(b, f, c), a.flags(), a.annotations(), parser.nf.CanonicalTypeNode(parser.pos(b), parser.ts.Void()), parser.nf.Id(parser.pos(c), c.getIdentifier()), e, h, null, new LinkedList<ParamTypeNode>(), i); :}
        |       
    modifiers_or_annotations_opt:a LT:b type_parameter_list_1:c VOID:d IDENTIFIER:i LPAREN
                formal_parameter_list_opt:e RPAREN:f dims_opt:g where_opt:j throws_opt:h
        {: RESULT = parser.nf.MethodDecl(parser.pos(d, f, i), a.flags(), a.annotations(), parser.nf.CanonicalTypeNode(parser.pos(d), parser.ts.Void()), parser.nf.Id(parser.pos(i), i.getIdentifier()), e, h, null, c, j); :}
        |
    // reconstruct
    COMP:a RECONSTRUCTOR:b LPAREN RPAREN:d
        {: 
        RESULT = parser.nf.ReconstructDecl(parser.pos(a, d), 
        				parser.nf.MPattern(parser.pos(a), parser.nf.CanonicalTypeNode(parser.pos(a), parser.ts.Object()) /*return type*/),
        				new Id_c(parser.pos(a, b), Names.RECONSTRUCT_METHOD_NAME)); :}
    |              // attribute 
    COMP:a ATTRIBUTER:b LPAREN RPAREN:c
        {: RESULT = parser.nf.AttributeDecl(parser.pos(a, c), parser.nf.CanonicalTypeNode(parser.pos(a), parser.ts.modeVT()) /*return type*/, new Id_c(parser.pos(a, b), Names.ATTRIBUTER_METHOD_NAME)); :} 
       ;
 
where_opt ::=
     {: RESULT = new LinkedList(); :}
    | WHERE mode_relation_list:a
     {: RESULT = a; :}
     ;
     
mode_relation_list ::=
     mode_relation:a
     {: RESULT = a; :}
   | mode_relation_list:a COMMA mode_relation:b
     {:  a.addAll(b);
         RESULT = a; :}
     ;
mode_relation ::=
     energy_name:a PARORD energy_name:b
     {: List<String> l = new LinkedList<String>();
        l.add(a.toString() + "<:" + b.toString());  
       RESULT = l; :}
     ;
     
//MPatternApp

mpattern_application ::=
	// (expression of type modeV) |> name of mpattern type or the patter itself
	IDENTIFIER:a MPAT_APP unary_expression_not_plus_minus:b
			{: RESULT = parser.nf.MPatternApp(parser.pos(a, b), a.getIdentifier(), b); :}
	;
	
    
//Pattern_Init_c
mpattern_init ::= 
          LBRACE mpattern_elements:a RBRACE
           {:    RESULT = parser.nf.MPattern_Init(parser.pos(a), a); :}
           ;

//List
mpattern_elements ::= 
		mpattern_element:a
			{:  List l = new LinkedList<MPatternElement>();
				l.add(a);
				RESULT = l;
			:}
	|	mpattern_elements:a COMMA mpattern_element:b
	        {: RESULT = a;
	        	a.add(b); :}
	;

//MPatternElement
mpattern_element ::=
		  name:a COLON:b unary_expression_not_plus_minus_nn:c
		  {:  RESULT = parser.nf.MPatternElement(parser.pos(a, c), a.toString(), c);	  	
		   :}
		  ;
		  
 // mswitch syntax

extend switch_statement ::=
		ATTRIBUTE:n LPAREN expression:a RPAREN:rprn mswitch_block:b
            {: 
               Position endpos = parser.pos(n, b);
               if (endpos == null) endpos = parser.pos(n, rprn);
               RESULT = ((etNodeFactory)parser.nf).MSwitch(endpos, a, b); 
            :}
    ;

mswitch_block ::=
                    // List of SwitchGroup
       LBRACE mswitch_block_statement_groups:a RBRACE
            {: RESULT = a; :} 
    ;
mswitch_block_statement_groups ::=
                    // List of SwitchGroup
        mswitch_block_statement_group:a
            {: RESULT = a; :}        
    |   mswitch_block_statement_groups:a mswitch_block_statement_group:b
            {: RESULT = a;
               a.addAll(b); :}
    ;
mswitch_block_statement_group ::=
                    //SwitchGroup
                   
     //   mswitch_label:a block_statements:b
     //       {:  List l = new LinkedList();
     //           l.add(parser.nf.MSwitchGroup(a, parser.nf.Block(parser.pos(a, b), b), parser.pos(a, b)));
     //           RESULT = l; :}
      
      mswitch_label:a expression:b SEMICOLON:c
            {:  List l = new LinkedList();
                l.add(parser.nf.MSwitchGroup(a, b, parser.pos(a, b)));
                RESULT = l; :}
    ;
mswitch_label ::=
        CASE:n MODE_LITERAL:a COLON:d
            {: RESULT = parser.nf.ModeLit(parser.pos(a), a.getValue()); :}
    |   DETERMINE:n COLON:d
            {: RESULT = parser.nf.ModeLit(parser.pos(n, d), "#DEFAULT"); :}
    ;

 
extend unary_expression_not_plus_minus_nn ::=
     adapt_expression:a
            {: RESULT = a; :}
  |  reconstruct_expression:a
 		{: RESULT = a; :}
 		
  |  attribute_expression:a
        {: RESULT = a; :}
  |  mpattern_init:a
  			{: RESULT = a; :}
  |   mpattern_application:a
  			{: RESULT = a;  :}
;

adapt_expression ::=
                    // Adapt expression
       ADAPT unary_expression_not_plus_minus:a 
            {: RESULT = parser.nf.Adapt(parser.pos(a), null, a); :}
		|	ADAPT:t LBRACK name:d RBRACK:b unary_expression_not_plus_minus:a 
			{: RESULT = parser.nf.Adapt(parser.pos(t, a), d.toString(), a); :}
	;

reconstruct_expression ::=
	   RECONSTRUCT unary_expression_not_plus_minus:a AS unary_expression_not_plus_minus:b
	         {:  RESULT = parser.nf.Reconstruct(parser.pos(a, b), a, b); :}
	        ;
  
attribute_expression ::=
                    // attribute_expression
      ATTRIBUTE unary_expression_not_plus_minus:a
            {: RESULT = parser.nf.Attribute(parser.pos(a), a); :}
            ;

// Class level energy type declare
override class_declaration ::=
		// record mode and phase value	
                    // ClassDecl
         modifiers_or_annotations_opt:a CLASS:n IDENTIFIER:b at_opt: g type_parameters_opt:c
                super_opt:d interfaces_opt:e class_body:f
            {: RESULT = parser.nf.ClassDecl(parser.pos(n, e),
                a.flags(), a.annotations(), parser.nf.Id(parser.pos(b), b.getIdentifier()), d, e, f, c, g); :}
  
         ;
	
override interface_declaration ::=
		modifiers_or_annotations_opt:a INTERFACE:b IDENTIFIER:c at_opt: g type_parameters_opt:d extends_interfaces_opt:e interface_body:f
        {: RESULT = parser.nf.ClassDecl(parser.pos(b, f), a.setFlag(Flags.INTERFACE).flags(), a.annotations(), parser.nf.Id(parser.pos(c), c.getIdentifier()), null, e, f, d, g); :}
    | AT:a INTERFACE:b IDENTIFIER:c at_opt: g annotation_body:d
        {: List superints = new LinkedList<TypeNode>();
           superints.add(parser.nf.CanonicalTypeNode(parser.pos(b, d), parser.ts.Annotation()));
           RESULT = parser.nf.ClassDecl(parser.pos(a, d), Flags.INTERFACE.set(JL5Flags.ANNOTATION), null, parser.nf.Id(parser.pos(c), c.getIdentifier()), null, superints, d, null, g); :}
    | modifiers_or_annotations:a AT:b INTERFACE:c at_opt: g IDENTIFIER:d annotation_body:e
        {: List superints = new LinkedList<TypeNode>();
           superints.add(parser.nf.CanonicalTypeNode(parser.pos(b, e), parser.ts.Annotation()));
           RESULT = parser.nf.ClassDecl(parser.pos(a, d), a.setFlag(Flags.INTERFACE.set(JL5Flags.ANNOTATION)).flags(), a.annotations(), parser.nf.Id(parser.pos(d), d.getIdentifier()), null, superints, e, null, g); :}
	;
// Mode and Phase declare

extend type_declaration ::=
                    // ClassDecl
	 MODES LBRACE:n modes_declarations_opt:a RBRACE:b 
            {: ModesDecl decl = new ModesDecl(parser.pos(n, b), Flags.NONE, parser.nf.Id(parser.pos(n), "modes"),
            								 null, new LinkedList(), 
            								 parser.nf.ClassBody(parser.pos(n, b), new LinkedList()));
            	decl.setList(a);
            	RESULT = decl;
             :}
    |  PHASES LBRACE:n phases_declarations_opt:a RBRACE:b 
            {: PhasesDecl decl = new PhasesDecl(parser.pos(n, b), Flags.NONE, parser.nf.Id(parser.pos(n), "phases"),
            								 null, new LinkedList(), 
            								 parser.nf.ClassBody(parser.pos(n, b), new LinkedList()));
            	decl.setList(a);
            	RESULT = decl; :}    
  /*  | EPOLICY name:a LBRACE:n epolicy_declarations_opt:b RBRACE:m
    		 {: EPolicyDecl decl = new EPolicyDecl(parser.pos(a, m), Flags.NONE, parser.nf.Id(parser.pos(n), "epolicies"),
            								 null, new LinkedList(), 
            								 parser.nf.ClassBody(parser.pos(a, m), new LinkedList()));
            	decl.setList(b);
            	RESULT = decl; :}
  */
    ; 

/*
epolicy_declarations_opt ::= 
	// List of policies
 	{: RESULT = new ArrayList(); :}
    |   epolicy_body_declarations:a
            {: RESULT = a; :}
    ;
epolicy_body_declarations ::= 
	epolicy_body_declaration:a
		{: RESULT = a; :}
	|  epolicy_body_declarations:a epolicy_body_declaration:b
			 {: RESULT = a;
			 	a.addAll(b);
			  :}
    ;
 
epolicy_body_declaration ::=
	name:b ESHIFT:c name:d SEMICOLON:e
            {: List l = new ArrayList();
            	l.add(b.toString() + "->" + d.toString());
                RESULT = l; :}
    ;
*/

modes_declarations_opt ::= 
	// List of Mode
 	{: RESULT = new ArrayList(); :}
    |   mode_body_declarations:a
            {: RESULT = a; :}
    ;
mode_body_declarations ::= 
	mode_body_declaration:a
		{: RESULT = a; :}
	|  mode_body_declarations:a mode_body_declaration:b
			 {: RESULT = a;
			 	a.addAll(b);
			  :}
    ;
 
mode_body_declaration ::=
	 name:b SEMICOLON:c
            {: List l = new ArrayList();   
            	l.add(b.toString());
             	RESULT = l; :}
    |  name:b PARORD:c name:d SEMICOLON:e
            {: List l = new ArrayList();
            	l.add(b.toString() + "<:" + d.toString());
                RESULT = l; :}
    ;

phases_declarations_opt ::= 
	// List of Mode
 	{: RESULT = new ArrayList(); :}
    |   phase_body_declarations:a
            {: RESULT = a; :}
    ;
phase_body_declarations ::= 
	phase_body_declaration:a
		{: RESULT = a; :}
	|  phase_body_declarations:a phase_body_declaration:b
			 {: RESULT = a;
			 	a.addAll(b);
			  :}
    ;
 
phase_body_declaration ::=
	name:b SEMICOLON:c
            {: List l = new ArrayList();   
            	l.add(b.toString());
             	RESULT = l;  :}
	|	name:b LTP:c name:d SEMICOLON:e
            {: List l = new ArrayList();
            	l.add(b.toString() + "<cpu" + d.toString());
                RESULT = l; :}
    ;
